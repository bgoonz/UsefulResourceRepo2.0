<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml" lang="" xml:lang="">
<head>
  <meta charset="utf-8" />
  <meta name="generator" content="pandoc" />
  <meta name="viewport" content="width=device-width, initial-scale=1.0, user-scalable=yes" />
  <title>groovy</title>
  <style type="text/css">
      code{white-space: pre-wrap;}
      span.smallcaps{font-variant: small-caps;}
      span.underline{text-decoration: underline;}
      div.column{display: inline-block; vertical-align: top; width: 50%;}
  </style>
</head>
<body>
<p>Groovy - A dynamic language for the Java platform <a href="http://www.groovy-lang.org/">Read more here.</a></p>
<pre class="groovy"><code>
/*
  Set yourself up:

  1) Install SDKMAN - http://sdkman.io/
  2) Install Groovy: sdk install groovy
  3) Start the groovy console by typing: groovyConsole

*/

//  Single line comments start with two forward slashes
/*
Multi line comments look like this.
*/

// Hello World
println &quot;Hello world!&quot;

/*
  Variables:

  You can assign values to variables for later use
*/

def x = 1
println x

x = new java.util.Date()
println x

x = -3.1499392
println x

x = false
println x

x = &quot;Groovy!&quot;
println x

/*
  Collections and maps
*/

//Creating an empty list
def technologies = []

/*** Adding a elements to the list ***/

// As with Java
technologies.add(&quot;Grails&quot;)

// Left shift adds, and returns the list
technologies &lt;&lt; &quot;Groovy&quot;

// Add multiple elements
technologies.addAll([&quot;Gradle&quot;,&quot;Griffon&quot;])

/*** Removing elements from the list ***/

// As with Java
technologies.remove(&quot;Griffon&quot;)

// Subtraction works also
technologies = technologies - &#39;Grails&#39;

/*** Iterating Lists ***/

// Iterate over elements of a list
technologies.each { println &quot;Technology: $it&quot;}
technologies.eachWithIndex { it, i -&gt; println &quot;$i: $it&quot;}

/*** Checking List contents ***/

//Evaluate if a list contains element(s) (boolean)
contained = technologies.contains( &#39;Groovy&#39; )

// Or
contained = &#39;Groovy&#39; in technologies

// Check for multiple contents
technologies.containsAll([&#39;Groovy&#39;,&#39;Grails&#39;])

/*** Sorting Lists ***/

// Sort a list (mutates original list)
technologies.sort()

// To sort without mutating original, you can do:
sortedTechnologies = technologies.sort( false )

/*** Manipulating Lists ***/

//Replace all elements in the list
Collections.replaceAll(technologies, &#39;Gradle&#39;, &#39;gradle&#39;)

//Shuffle a list
Collections.shuffle(technologies, new Random())

//Clear a list
technologies.clear()

//Creating an empty map
def devMap = [:]

//Add values
devMap = [&#39;name&#39;:&#39;Roberto&#39;, &#39;framework&#39;:&#39;Grails&#39;, &#39;language&#39;:&#39;Groovy&#39;]
devMap.put(&#39;lastName&#39;,&#39;Perez&#39;)

//Iterate over elements of a map
devMap.each { println &quot;$it.key: $it.value&quot; }
devMap.eachWithIndex { it, i -&gt; println &quot;$i: $it&quot;}

//Evaluate if a map contains a key
assert devMap.containsKey(&#39;name&#39;)

//Evaluate if a map contains a value
assert devMap.containsValue(&#39;Roberto&#39;)

//Get the keys of a map
println devMap.keySet()

//Get the values of a map
println devMap.values()

/*
  Groovy Beans

  GroovyBeans are JavaBeans but using a much simpler syntax

  When Groovy is compiled to bytecode, the following rules are used.

    * If the name is declared with an access modifier (public, private or
      protected) then a field is generated.

    * A name declared with no access modifier generates a private field with
      public getter and setter (i.e. a property).

    * If a property is declared final the private field is created final and no
      setter is generated.

    * You can declare a property and also declare your own getter or setter.

    * You can declare a property and a field of the same name, the property will
      use that field then.

    * If you want a private or protected property you have to provide your own
      getter and setter which must be declared private or protected.

    * If you access a property from within the class the property is defined in
      at compile time with implicit or explicit this (for example this.foo, or
      simply foo), Groovy will access the field directly instead of going though
      the getter and setter.

    * If you access a property that does not exist using the explicit or
      implicit foo, then Groovy will access the property through the meta class,
      which may fail at runtime.

*/

class Foo {
    // read only property
    final String name = &quot;Roberto&quot;

    // read only property with public getter and protected setter
    String language
    protected void setLanguage(String language) { this.language = language }

    // dynamically typed property
    def lastName
}

/*
  Methods with optional parameters
*/

// A method can have default values for parameters
def say(msg = &#39;Hello&#39;, name = &#39;world&#39;) {
    &quot;$msg $name!&quot;
}

// It can be called in 3 different ways
assert &#39;Hello world!&#39; == say()
// Right most parameter with default value is eliminated first.
assert &#39;Hi world!&#39; == say(&#39;Hi&#39;)
assert &#39;learn groovy&#39; == say(&#39;learn&#39;, &#39;groovy&#39;)

/*
  Logical Branching and Looping
*/

//Groovy supports the usual if - else syntax
def x = 3

if(x==1) {
    println &quot;One&quot;
} else if(x==2) {
    println &quot;Two&quot;
} else {
    println &quot;X greater than Two&quot;
}

//Groovy also supports the ternary operator:
def y = 10
def x = (y &gt; 1) ? &quot;worked&quot; : &quot;failed&quot;
assert x == &quot;worked&quot;

//Groovy supports &#39;The Elvis Operator&#39; too!
//Instead of using the ternary operator:

displayName = user.name ? user.name : &#39;Anonymous&#39;

//We can write it:
displayName = user.name ?: &#39;Anonymous&#39;

//For loop
//Iterate over a range
def x = 0
for (i in 0 .. 30) {
    x += i
}

//Iterate over a list
x = 0
for( i in [5,3,2,1] ) {
    x += i
}

//Iterate over an array
array = (0..20).toArray()
x = 0
for (i in array) {
    x += i
}

//Iterate over a map
def map = [&#39;name&#39;:&#39;Roberto&#39;, &#39;framework&#39;:&#39;Grails&#39;, &#39;language&#39;:&#39;Groovy&#39;]
x = &quot;&quot;
for ( e in map ) {
    x += e.value
    x += &quot; &quot;
}
assert x.equals(&quot;Roberto Grails Groovy &quot;)

/*
  Operators

  Operator Overloading for a list of the common operators that Groovy supports:
  http://www.groovy-lang.org/operators.html#Operator-Overloading

  Helpful groovy operators
*/
//Spread operator:  invoke an action on all items of an aggregate object.
def technologies = [&#39;Groovy&#39;,&#39;Grails&#39;,&#39;Gradle&#39;]
technologies*.toUpperCase() // = to technologies.collect { it?.toUpperCase() }

//Safe navigation operator: used to avoid a NullPointerException.
def user = User.get(1)
def username = user?.username


/*
  Closures
  A Groovy Closure is like a &quot;code block&quot; or a method pointer. It is a piece of
  code that is defined and then executed at a later point.

  More info at: http://www.groovy-lang.org/closures.html
*/
//Example:
def clos = { println &quot;Hello World!&quot; }

println &quot;Executing the Closure:&quot;
clos()

//Passing parameters to a closure
def sum = { a, b -&gt; println a+b }
sum(2,4)

//Closures may refer to variables not listed in their parameter list.
def x = 5
def multiplyBy = { num -&gt; num * x }
println multiplyBy(10)

// If you have a Closure that takes a single argument, you may omit the
// parameter definition of the Closure
def clos = { print it }
clos( &quot;hi&quot; )

/*
  Groovy can memoize closure results [1][2][3]
*/
def cl = {a, b -&gt;
    sleep(3000) // simulate some time consuming processing
    a + b
}

mem = cl.memoize()

def callClosure(a, b) {
    def start = System.currentTimeMillis()
    mem(a, b)
    println &quot;Inputs(a = $a, b = $b) - took ${System.currentTimeMillis() - start} msecs.&quot;
}

callClosure(1, 2)
callClosure(1, 2)
callClosure(2, 3)
callClosure(2, 3)
callClosure(3, 4)
callClosure(3, 4)
callClosure(1, 2)
callClosure(2, 3)
callClosure(3, 4)

/*
  Expando

  The Expando class is a dynamic bean so we can add properties and we can add
  closures as methods to an instance of this class

  http://mrhaki.blogspot.mx/2009/10/groovy-goodness-expando-as-dynamic-bean.html
*/
  def user = new Expando(name:&quot;Roberto&quot;)
  assert &#39;Roberto&#39; == user.name

  user.lastName = &#39;Pérez&#39;
  assert &#39;Pérez&#39; == user.lastName

  user.showInfo = { out -&gt;
      out &lt;&lt; &quot;Name: $name&quot;
      out &lt;&lt; &quot;, Last name: $lastName&quot;
  }

  def sw = new StringWriter()
  println user.showInfo(sw)


/*
  Metaprogramming (MOP)
*/

//Using ExpandoMetaClass to add behaviour
String.metaClass.testAdd = {
    println &quot;we added this&quot;
}

String x = &quot;test&quot;
x?.testAdd()

//Intercepting method calls
class Test implements GroovyInterceptable {
    def sum(Integer x, Integer y) { x + y }

    def invokeMethod(String name, args) {
        System.out.println &quot;Invoke method $name with args: $args&quot;
    }
}

def test = new Test()
test?.sum(2,3)
test?.multiply(2,3)

//Groovy supports propertyMissing for dealing with property resolution attempts.
class Foo {
   def propertyMissing(String name) { name }
}
def f = new Foo()

assertEquals &quot;boo&quot;, f.boo

/*
  TypeChecked and CompileStatic
  Groovy, by nature, is and will always be a dynamic language but it supports
  typechecked and compilestatic

  More info: http://www.infoq.com/articles/new-groovy-20
*/
//TypeChecked
import groovy.transform.TypeChecked

void testMethod() {}

@TypeChecked
void test() {
    testMeethod()

    def name = &quot;Roberto&quot;

    println naameee

}

//Another example:
import groovy.transform.TypeChecked

@TypeChecked
Integer test() {
    Integer num = &quot;1&quot;

    Integer[] numbers = [1,2,3,4]

    Date date = numbers[1]

    return &quot;Test&quot;

}

//CompileStatic example:
import groovy.transform.CompileStatic

@CompileStatic
int sum(int x, int y) {
    x + y
}

assert sum(2,5) == 7

</code></pre>
<h2 id="further-resources">Further resources</h2>
<p><a href="http://www.groovy-lang.org/documentation.html">Groovy documentation</a></p>
<p><a href="http://groovyconsole.appspot.com/">Groovy web console</a></p>
<p>Join a <a href="http://www.groovy-lang.org/usergroups.html">Groovy user group</a></p>
<h2 id="books">Books</h2>
<ul>
<li><p>[Groovy Goodness] (https://leanpub.com/groovy-goodness-notebook)</p></li>
<li><p>[Groovy in Action] (http://manning.com/koenig2/)</p></li>
<li><p>[Programming Groovy 2: Dynamic Productivity for the Java Developer] (http://shop.oreilly.com/product/9781937785307.do)</p></li>
</ul>
<p>[1] http://roshandawrani.wordpress.com/2010/10/18/groovy-new-feature-closures-can-now-memorize-their-results/ [2] http://www.solutionsiq.com/resources/agileiq-blog/bid/72880/Programming-with-Groovy-Trampoline-and-Memoize [3] http://mrhaki.blogspot.mx/2011/05/groovy-goodness-cache-closure-results.html</p>
</body>
</html>
