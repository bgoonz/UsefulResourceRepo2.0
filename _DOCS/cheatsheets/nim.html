<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml" lang="" xml:lang="">
<head>
  <meta charset="utf-8" />
  <meta name="generator" content="pandoc" />
  <meta name="viewport" content="width=device-width, initial-scale=1.0, user-scalable=yes" />
  <title>nim</title>
  <style type="text/css">
      code{white-space: pre-wrap;}
      span.smallcaps{font-variant: small-caps;}
      span.underline{text-decoration: underline;}
      div.column{display: inline-block; vertical-align: top; width: 50%;}
  </style>
</head>
<body>
<p>Nim (formerly Nimrod) is a statically typed, imperative programming language that gives the programmer power without compromises on runtime efficiency.</p>
<p>Nim is efficient, expressive, and elegant.</p>
<pre class="nim"><code># Single-line comments start with a #

#[
  This is a multiline comment.
  In Nim, multiline comments can be nested, beginning with #[
  ... and ending with ]#
]#

discard &quot;&quot;&quot;
This can also work as a multiline comment.
Or for unparsable, broken code
&quot;&quot;&quot;

var                     # Declare (and assign) variables,
  letter: char = &#39;n&#39;    # with or without type annotations
  lang = &quot;N&quot; &amp; &quot;im&quot;
  nLength: int = len(lang)
  boat: float
  truth: bool = false

let            # Use let to declare and bind variables *once*.
  legs = 400   # legs is immutable.
  arms = 2_000 # _ are ignored and are useful for long numbers.
  aboutPi = 3.15

const            # Constants are computed at compile time. This provides
  debug = true   # performance and is useful in compile time expressions.
  compileBadCode = false

when compileBadCode:            # `when` is a compile time `if`
  legs = legs + 1               # This error will never be compiled.
  const input = readline(stdin) # Const values must be known at compile time.

discard 1 &gt; 2 # Note: The compiler will complain if the result of an expression
              # is unused. `discard` bypasses this.


#
# Data Structures
#

# Tuples

var
  child: tuple[name: string, age: int]   # Tuples have *both* field names
  today: tuple[sun: string, temp: float] # *and* order.

child = (name: &quot;Rudiger&quot;, age: 2) # Assign all at once with literal ()
today.sun = &quot;Overcast&quot;            # or individual fields.
today.temp = 70.1

# Sequences

var
  drinks: seq[string]

drinks = @[&quot;Water&quot;, &quot;Juice&quot;, &quot;Chocolate&quot;] # @[V1,..,Vn] is the sequence literal

drinks.add(&quot;Milk&quot;)

if &quot;Milk&quot; in drinks:
  echo &quot;We have Milk and &quot;, drinks.len - 1, &quot; other drinks&quot;

let myDrink = drinks[2]

#
# Defining Types
#

# Defining your own types puts the compiler to work for you. It&#39;s what makes
# static typing powerful and useful.

type
  Name = string # A type alias gives you a new type that is interchangeable
  Age = int     # with the old type but is more descriptive.
  Person = tuple[name: Name, age: Age] # Define data structures too.
  AnotherSyntax = tuple
    fieldOne: string
    secondField: int

var
  john: Person = (name: &quot;John B.&quot;, age: 17)
  newage: int = 18 # It would be better to use Age than int

john.age = newage # But still works because int and Age are synonyms

type
  Cash = distinct int    # `distinct` makes a new type incompatible with its
  Desc = distinct string # base type.

var
  money: Cash = 100.Cash # `.Cash` converts the int to our type
  description: Desc  = &quot;Interesting&quot;.Desc

when compileBadCode:
  john.age  = money        # Error! age is of type int and money is Cash
  john.name = description  # Compiler says: &quot;No way!&quot;

#
# More Types and Data Structures
#

# Enumerations allow a type to have one of a limited number of values

type
  Color = enum cRed, cBlue, cGreen
  Direction = enum # Alternative formatting
    dNorth
    dWest
    dEast
    dSouth
var
  orient = dNorth # `orient` is of type Direction, with the value `dNorth`
  pixel = cGreen # `pixel` is of type Color, with the value `cGreen`

discard dNorth &gt; dEast # Enums are usually an &quot;ordinal&quot; type

# Subranges specify a limited valid range

type
  DieFaces = range[1..20] # Only an int from 1 to 20 is a valid value
var
  my_roll: DieFaces = 13

when compileBadCode:
  my_roll = 23 # Error!

# Arrays

type
  RollCounter = array[DieFaces, int]  # Array&#39;s are fixed length and
  DirNames = array[Direction, string] # indexed by any ordinal type.
  Truths = array[42..44, bool]
var
  counter: RollCounter
  directions: DirNames
  possible: Truths

possible = [false, false, false] # Literal arrays are created with [V1,..,Vn]
possible[42] = true

directions[dNorth] = &quot;Ahh. The Great White North!&quot;
directions[dWest] = &quot;No, don&#39;t go there.&quot;

my_roll = 13
counter[my_roll] += 1
counter[my_roll] += 1

var anotherArray = [&quot;Default index&quot;, &quot;starts at&quot;, &quot;0&quot;]

# More data structures are available, including tables, sets, lists, queues,
# and crit bit trees.
# http://nim-lang.org/docs/lib.html#collections-and-algorithms

#
# IO and Control Flow
#

# `case`, `readLine()`

echo &quot;Read any good books lately?&quot;
case readLine(stdin)
of &quot;no&quot;, &quot;No&quot;:
  echo &quot;Go to your local library.&quot;
of &quot;yes&quot;, &quot;Yes&quot;:
  echo &quot;Carry on, then.&quot;
else:
  echo &quot;That&#39;s great; I assume.&quot;

# `while`, `if`, `continue`, `break`

import strutils as str # http://nim-lang.org/docs/strutils.html
echo &quot;I&#39;m thinking of a number between 41 and 43. Guess which!&quot;
let number: int = 42
var
  raw_guess: string
  guess: int
while guess != number:
  raw_guess = readLine(stdin)
  if raw_guess == &quot;&quot;: continue # Skip this iteration
  guess = str.parseInt(raw_guess)
  if guess == 1001:
    echo(&quot;AAAAAAGGG!&quot;)
    break
  elif guess &gt; number:
    echo(&quot;Nope. Too high.&quot;)
  elif guess &lt; number:
    echo(guess, &quot; is too low&quot;)
  else:
    echo(&quot;Yeeeeeehaw!&quot;)

#
# Iteration
#

for i, elem in [&quot;Yes&quot;, &quot;No&quot;, &quot;Maybe so&quot;]: # Or just `for elem in`
  echo(elem, &quot; is at index: &quot;, i)

for k, v in items(@[(person: &quot;You&quot;, power: 100), (person: &quot;Me&quot;, power: 9000)]):
  echo v

let myString = &quot;&quot;&quot;
an &lt;example&gt;
`string` to
play with
&quot;&quot;&quot; # Multiline raw string

for line in splitLines(myString):
  echo(line)

for i, c in myString:       # Index and letter. Or `for j in` for just letter
  if i mod 2 == 0: continue # Compact `if` form
  elif c == &#39;X&#39;: break
  else: echo(c)

#
# Procedures
#

type Answer = enum aYes, aNo

proc ask(question: string): Answer =
  echo(question, &quot; (y/n)&quot;)
  while true:
    case readLine(stdin)
    of &quot;y&quot;, &quot;Y&quot;, &quot;yes&quot;, &quot;Yes&quot;:
      return Answer.aYes  # Enums can be qualified
    of &quot;n&quot;, &quot;N&quot;, &quot;no&quot;, &quot;No&quot;:
      return Answer.aNo
    else: echo(&quot;Please be clear: yes or no&quot;)

proc addSugar(amount: int = 2) = # Default amount is 2, returns nothing
  assert(amount &gt; 0 and amount &lt; 9000, &quot;Crazy Sugar&quot;)
  for a in 1..amount:
    echo(a, &quot; sugar...&quot;)

case ask(&quot;Would you like sugar in your tea?&quot;)
of aYes:
  addSugar(3)
of aNo:
  echo &quot;Oh do take a little!&quot;
  addSugar()
# No need for an `else` here. Only `yes` and `no` are possible.

#
# FFI
#

# Because Nim compiles to C, FFI is easy:

proc strcmp(a, b: cstring): cint {.importc: &quot;strcmp&quot;, nodecl.}

let cmp = strcmp(&quot;C?&quot;, &quot;Easy!&quot;)</code></pre>
<p>Additionally, Nim separates itself from its peers with metaprogramming, performance, and compile-time features.</p>
<h2 id="further-reading">Further Reading</h2>
<ul>
<li><a href="http://nim-lang.org">Home Page</a></li>
<li><a href="http://nim-lang.org/download.html">Download</a></li>
<li><a href="http://nim-lang.org/community.html">Community</a></li>
<li><a href="http://nim-lang.org/question.html">FAQ</a></li>
<li><a href="http://nim-lang.org/documentation.html">Documentation</a></li>
<li><a href="http://nim-lang.org/docs/manual.html">Manual</a></li>
<li><a href="http://nim-lang.org/docs/lib.html">Standard Library</a></li>
<li><a href="http://rosettacode.org/wiki/Category:Nim">Rosetta Code</a></li>
</ul>
</body>
</html>
