<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml" lang="" xml:lang="">
<head>
  <meta charset="utf-8" />
  <meta name="generator" content="pandoc" />
  <meta name="viewport" content="width=device-width, initial-scale=1.0, user-scalable=yes" />
  <title>janet</title>
  <style type="text/css">
      code{white-space: pre-wrap;}
      span.smallcaps{font-variant: small-caps;}
      span.underline{text-decoration: underline;}
      div.column{display: inline-block; vertical-align: top; width: 50%;}
  </style>
</head>
<body>
<p><a href="https://janet-lang.org/">Janet</a> is a Lisp-like (Clojure-like), lexically-scoped, dynamically-typed, garbage-collected, C-based, high-level language. The entire language (core library, interpreter, compiler, assembler, PEG) is about 300-500 kB and should run on many constrained systems.</p>
<p>I encourage you to try out the code snippets below in the Janet repl (either by <a href="https://janet-lang.org/docs/index.html">installing Janet</a>, or else by using the repl embedded in the Janet homepage).</p>
<p>As we only have a scant <em>y</em> minutes, we’ll survey the basics here and leave the remaining details for the manual. So please, keep your arms and legs inside the vehicle at all times, and on with the scenic tour!</p>
<pre class="janet"><code># A comment.

# Some literal values.
true
false
nil

# Typical style for symbols (identifiers-for / names-of things).
do-stuff
pants-on-fire!
foo-&gt;bar  # Evidently for converting foos to bars.
fully-charged?
_  # Usually used as a dummy variable.

# Keywords are like symbols that start with a colon, are treated like
# constants, and are typically used as map keys or pieces of syntax in
# macros.
:a
:some-val

# Numbers #####################################################################
5
1e3    # =&gt; 1000
1_000  # =&gt; 1000
2e-03  # =&gt; 0.002
0xff   # =&gt; 255

# You can specify a radix (base) like so:
16rff   # =&gt; 255 (same as 0xff)
2r1101  # =&gt;  13

# Some numbers in the math library:
math/pi  # =&gt; 3.14159
math/e   # =&gt; 2.71828

# Strings #####################################################################
&quot;hello&quot;
&quot;hey\tthere&quot;  # contains a tab

# For multi-line strings, use one or more backticks. No escapes allowed.
``a long
multi-line
string``  # =&gt; &quot;a long\nmulti-line\nstring&quot;

# Strings and data structures in Janet come in two varieties: mutable and
# immutable. The literal for the mutable variety is written with a `@` in
# front of it.

# A mutable string (aka &quot;buffer&quot;).
@&quot;this&quot;
@`a multi-line
one here`

(string &quot;con&quot; &quot;cat&quot; &quot;enate&quot;)  # =&gt; &quot;concatenate&quot;

# To get a substring:
(string/slice &quot;abcdefgh&quot; 2 5)  # =&gt; &quot;cde&quot;
# To find a substring:
(string/find &quot;de&quot; &quot;abcdefgh&quot;)  # =&gt; 3

# See the string library for more (splitting, replacement, etc.)

# Arrays and Tuples ###########################################################
# Arrays are mutable, tuples are immutable.

# Arrays (mutable)
@(4 5 6)
@[4 5 6]

# Tuples (immutable)
# Note that an open paren usually indicates a function call, so if you want a
# literal tuple with parens, you need to &quot;quote&quot; it (with a starting single
# quote mark).
&#39;(4 5 6)
[4 5 6]  # ... or just use square brackets.

# Tables and Structs (AKA: &quot;maps&quot;, &quot;hashmaps&quot;, &quot;dictionaries&quot;)
@{:a 1 :b 2 :c 3}  # table  (mutable)
{:a 1 :b 2 :c 3}   # struct (immutable)

# More about how to work with arrays/tuples and tables/structs below.

# Bindings ####################################################################
# ... or &quot;Name Some Things!&quot; (that is, bind a value to a symbol)
(def x 4.7)  # Define a constant, `x`.
x            # =&gt; 4.7
(quote x)    # =&gt; x (the symbol x)
&#39;x           # =&gt; x (the symbol x (shorthand))
(print x)    # prints 4.7

# Since we used `def`, can&#39;t change to what `x` refers:
(set x 5.6)  # Error, `x` is a constant.

(var y 10)
(set y 12)  # Works, since `y` was made var.

# Note that bindings are local to the scope they&#39;re called in. `let`
# creates a local scope and makes some bindings all in one shot:
(let [a 2
      b 3]
  (print &quot;Hello from inside this local scope.&quot;)
  (* a b))  # =&gt; 6

# Destructuring is supported, both for arrays/tuples ...
(def a [&quot;foos&quot; &quot;bars&quot; &quot;moos&quot;])
(let [[s1 _ s2] a]
  (print s1 s2))  # foosmoos

# ... and for tables/structs.
(def t {:a &quot;ayy&quot; :b &quot;bee&quot; :c &quot;sea&quot;})
(let [{:a a :b b} t]
  (print a b))  # ayybee

# You can even destructure right in a `def`:
(def [aa1 aa2] a)
aa1  # =&gt; foos
aa2  # =&gt; bars

(def {:c body-of-water :b insect-friend} t)
body-of-water  # =&gt; sea
insect-friend  # =&gt; bee

# Note that keywords evaluate to themselves, whereas symbols evaluate
# to whatever value they&#39;re bound to (unless you quote them).

# Operators ###################################################################
# Janet supports the usual ensemble of operators.
# +, -, *, /, and so on. Note:
(/ 5 3)  # =&gt;  1.66667
(% 5 3)  # =&gt;  2 (remainder)
(- 5)    # =&gt; -5 (or you can just write `-5`)

(++ i)    # increments
(-- i)    # decrements
(+= i 3)  # add 3 to `i`
(*= i 3)  # triple `i`
# ... and so on for the other operations on numbers.

# Comparison
# =  &lt;  &gt;  not=  &lt;=  &gt;=
(&lt; 2 7 12)  # =&gt; true

# Functions ###################################################################
# Call them:
(- 5 3)  # =&gt; 2 (Yes, operators and functions work the same.)
(math/sin (/ math/pi 2))  # =&gt; 1
(range 5)  # =&gt; @[0 1 2 3 4]

# Create them:
(defn mult-by-2
  ``First line of docstring.

  Some more of the docstring.

  Possibly more!``
  [x]
  (print &quot;Hi.&quot;)
  (print &quot;Will compute using: &quot; x)
  (* 2 x))

(print (mult-by-2 6))  # =&gt; 12 (after printing &quot;Hi&quot; and so forth)

# If you have a function named &quot;main&quot; in your file, `janet` will automatically
# call it for you when you run the file.

# Interactively read a function&#39;s docs from within the repl:
(doc mult-by-2)

# Note, functions have to be defined before they can be used in a function,
# so if you design top-down, you&#39;ll need to write your functions from the
# bottom of the file up.

# You can make anonymous functions as well:
(fn [x] (+ x x))
(fn my-func [x] (+ x x))  # This one&#39;s less anonymous.

# Use `do` to make some side-effecting calls and then evaluate to
# the last form in the `do`:
(def n (do
         (print &quot;hi&quot;)
         (do-some-side-effecting 42)
         3))
n  # =&gt; 3

# You might say that function bodies provide an &quot;implicit do&quot;.

# Operations on data structures ###############################################
# (Making all these mutable so we can ... mutate them.)
(def s @&quot;Hello, World!&quot;)
(def a @[:a :b :c :d :e])
(def t @{:a 1 :b 2})

(length s)  # =&gt; 13
(length a)  # =&gt;  5
(length t)  # =&gt;  2

# Getting values:
(s 7)   # =&gt; 87 (which is the code point for &quot;W&quot;)
(a 1)   # =&gt; :b
(t :a)  # =&gt; 1
(keys t)    # =&gt; @[:a :b]
(values t)  # =&gt; @[1 2]

# Changing values (for mutable data structures):
(put s 2 87)   # @&quot;HeWlo, World!&quot;
(put a 2 :x)   # @[:a :b :x :d :e]
(put t :b 42)  # @{:a 1 :b 42}

# Adding &amp; removing values (again, for mutable data structures):
(buffer/push-string s &quot;??&quot;)  # @&quot;HeWlo, World!??&quot;
(array/push a :f)  # @[:a :b :x :d :e :f]
(array/pop a)      # =&gt; :f, and it&#39;s also removed from `a`.
(put t :x 88)      # @{:a 1 :b 42 :x 88}

# See the manual for a wide variety of functions for working with
# buffers/strings, arrays/tuples, and tables/struct.

# Flow control ################################################################
(if some-condition
  42
  38)

# Only `nil` and `false` are falsey. Everything else is truthy.

(if got-it?
  71)  # No false-branch value. Returns `nil` if `got-it?` is falsey.

(var i 10)
(while (pos? i)
  (print &quot;... &quot; i)
  (-- i))
# Now `i` is 0.

# `case` compares the dispatch value to each of the options.
(var x 2)
(case x
  1 &quot;won&quot;
  2 &quot;too&quot;
  3 &quot;tree&quot;
  &quot;unknown&quot;)  # =&gt; &quot;too&quot;

# `cond` evaluates conditions until it gets a `true`.
(set x 8)
(cond
  (= x 1) &quot;won&quot;
  (= x 2) &quot;too&quot;
  (&lt; x 10) &quot;tree&quot;
  &quot;oof!&quot;)  # =&gt; &quot;tree&quot;

(when (avoided-wipeout?)
  (do-side-effecty-thing 88)
  (smell-the-roses)
  (paint-fencepost-error))

# Pattern matching.
# `match` is like a high-powered switch expression. If you switch on a data
# structure, it can look inside to try and match on its contents. For example,
# matching on a table or struct:
(def t {:a 1 :b 2 :c 3})
(match t
  {:yar v} (print &quot;matches key :yar! &quot; v)
  {:moo v} (print &quot;matches key :moo! &quot; v)
  {:c   v} (print &quot;matches key :c! &quot;   v)
  _ (print &quot;no match&quot;))  # =&gt; prints &quot;matches key :c! 3&quot;

# Iterating ###################################################################
# Iterate over an integer range:
(for i 0 5
  (print i))  # prints 0, 1, 2, 3, 4

# There&#39;s also the more general `loop`:
(loop [i :range [0 10] :when (even? i)]
  (print i))

# Loop over an array/tuple:
(def words [&quot;foo&quot; &quot;bar&quot; &quot;baz&quot;])
(each word words
  (print word))

# Loop over a table/struct:
(def t {:a 1 :b 2})
(eachp [k v] t  # Loop over each pair in `t`.
  (print k &quot; --&gt; &quot; v))

# Can also use `eachk` to loop over keys in a table or struct.

# Functional programming ######################################################
# You&#39;ll find many familiar old friends here.
(filter even?
        (map (fn [x]
               (* x x))
             (range 10)))  # =&gt; @[0 4 16 36 64]

(reduce + 0 (range 5))  # =&gt; 10

# ...and lots more (see the API docs).

# Errata ######################################################################
(type a)                # =&gt; the type of `a` (as a keyword)
(describe a)            # =&gt; a human-readable description of `a`
(string/format &quot;%j&quot; a)  # =&gt; Janet values, nicely-formatted</code></pre>
<p>This tour didn’t cover a number of other features such as modules, fibers, PEGs, macros, etc., but should give you a taste of what Janet is like. See the <a href="https://janet-lang.org/docs/index.html">Janet manual</a> and the <a href="https://janet-lang.org/api/index.html">Janet API docs</a> for more info.</p>
</body>
</html>
